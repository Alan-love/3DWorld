uniform vec3 offset, scale;
uniform float start_mag, start_freq, rx, ry;
uniform writeonly image3D dest_tex;

void main()
{
	vec3 pos   = offset + scale*vec3(gl_GlobalInvocationID.zxy); // Note: {x,y,z} is reordered to {z,x,y}
	float val  = 0.0;
	float mag  = start_mag;
	float freq = start_freq;
	float crx  = rx;
	float cry  = ry;
	const float lacunarity = 1.92;
	const float gain       = 0.5;
	const int NUM_OCTAVES  = 5;

	for (int i = 0; i < NUM_OCTAVES; ++i) {
		float noise = simplex(freq*pos + vec3(crx, cry, crx-cry));
		val  += mag*noise;
		mag  *= gain;
		freq *= lacunarity;
		crx  *= 1.5;
		cry  *= 1.5;
	}
	ivec3 store_pos = ivec3(gl_GlobalInvocationID);
	imageStore(dest_tex, store_pos, vec4(val, 0.0, 0.0, 0.0)); // only red channel is used
}
